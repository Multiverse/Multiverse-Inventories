plugins {
    id 'checkstyle'
    id 'org.mvplugins.multiverse-plugin' version '1.2.1'
}

group = 'org.mvplugins.multiverse.inventories'
description = 'Multiverse-Inventories'

repositories {
    maven {
        name = 'codemc'
        url = uri('https://repo.codemc.org/repository/maven-releases')
    }

    maven {
        name = "helpchatRepoReleases"
        url = uri("https://repo.helpch.at/releases/")
    }

    maven {
        name = 'benwoo1110'
        url = uri('https://repo.c0ding.party/multiverse-beta')
    }

    maven {
        name = "viaversion"
        url = uri("https://repo.viaversion.com")
    }
}

configure(apiDependencies) {
    serverApiVersion = '1.21.8-R0.1-SNAPSHOT'
    mockBukkitServerApiVersion = '1.21'
    mockBukkitVersion = '4.31.1'
}

dependencies {
    // Core
    externalPlugin 'org.mvplugins.multiverse.core:multiverse-core:5.2.0'

    // Config
    shadowed 'com.dumptruckman.minecraft:JsonConfiguration:1.2-SNAPSHOT'
    shadowed 'net.minidev:json-smart:2.6.0'

    // Utils
    shadowed('com.dumptruckman.minecraft:Logging:1.1.1') {
        exclude group: 'junit', module: 'junit'
    }
    shadowed 'com.viaversion:nbt:5.1.1'

    // Caching
    shadowed("com.github.ben-manes.caffeine:caffeine:3.2.2")

    // PlaceholderAPI
    externalPlugin 'me.clip:placeholderapi:2.11.6'

    // Luckperms for group context
    compileOnly 'net.luckperms:api:5.4'

    // Other plugins for import
    compileOnly('uk.co:MultiInv:3.0.6') {
        exclude group: '*', module: '*'
    }
    compileOnly('me.drayshak:WorldInventories:1.0.2') {
        exclude group: '*', module: '*'
    }
    // perworldinventory is weird and has snakeyaml included in the jar, so we can only use compileOnly for build to work properly
    compileOnly('me.ebonjaeger:perworldinventory-kt:2.3.2') {
        exclude group: '*', module: '*'
    }

    // hk2 for annotation processing only
    compileOnly('org.glassfish.hk2:hk2-api:3.1.1') {
        exclude group: '*', module: '*'
    }
    annotationProcessor 'org.glassfish.hk2:hk2-metadata-generator:3.1.1'
    testAnnotationProcessor 'org.glassfish.hk2:hk2-metadata-generator:3.1.1'
}

shadowJar {
    relocate 'com.dumptruckman.minecraft.util.Logging', 'org.mvplugins.multiverse.inventories.utils.InvLogging'
    relocate 'com.dumptruckman.minecraft.util.DebugLog', 'org.mvplugins.multiverse.inventories.utils.DebugFileLogger'
    relocate 'com.dumptruckman.bukkit.configuration', 'org.mvplugins.multiverse.inventories.utils.configuration'
    relocate 'net.minidev', 'org.mvplugins.multiverse.inventories.utils.minidev'
    relocate 'com.github.benmanes', 'org.mvplugins.multiverse.inventories.utils.benmanes'
    relocate 'com.google.errorprone', 'org.mvplugins.multiverse.inventories.utils.errorprone'

    dependencies {
        exclude(dependency {
            it.moduleGroup == 'org.ow2.asm'
        })
        exclude(dependency {
            it.moduleGroup == 'org.jetbrains'
        })
    }
}

checkstyle {
    toolVersion = '6.1.1'
    configFile file('config/mv_checks.xml')
    ignoreFailures = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Multiverse/Multiverse-Inventories"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
