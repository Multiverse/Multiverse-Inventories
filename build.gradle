plugins {
    id 'org.mvplugins.multiverse-plugin' version '1.0.0'
    id 'org.mvplugins.kotlin-test-only' version '1.0.0'
}

group = 'org.mvplugins.multiverse.inventories'
description = 'Multiverse-Inventories'

repositories {
    maven {
        name = 'jitpack.io'
        url = uri('https://jitpack.io/')
    }

    maven {
        name = 'benwoo1110'
        url = uri('https://repo.c0ding.party/multiverse-beta')
    }
}

configure(apiDependencies) {
    serverApiVersion = '1.21.4-R0.1-SNAPSHOT'
    mockBukkitServerApiVersion = '1.21'
}

dependencies {
    // Core
    // TODO update to correct version once we have it published
    externalPlugin 'org.mvplugins.multiverse.core:multiverse-core:5.0.0-SNAPSHOT'

    // Config
    shadowed 'com.dumptruckman.minecraft:JsonConfiguration:1.2-SNAPSHOT'
    shadowed 'net.minidev:json-smart:2.5.1'

    // Utils
    shadowed('com.dumptruckman.minecraft:Logging:1.1.1') {
        exclude group: 'junit', module: 'junit'
    }

    // Other plugins for import
    externalPlugin('uk.co:MultiInv:3.0.6') {
        exclude group: '*', module: '*'
    }
    externalPlugin('me.drayshak:WorldInventories:1.0.2') {
        exclude group: '*', module: '*'
    }

    // hk2 for annotation processing only
    compileOnly('org.glassfish.hk2:hk2-api:3.0.3') {
        exclude group: '*', module: '*'
    }
    annotationProcessor 'org.glassfish.hk2:hk2-metadata-generator:3.0.3'
    testAnnotationProcessor 'org.glassfish.hk2:hk2-metadata-generator:3.0.3'
}

shadowJar {
    relocate 'com.dumptruckman.minecraft.util.Logging', 'org.mvplugins.multiverse.inventories.utils.InvLogging'
    relocate 'com.dumptruckman.minecraft.util.DebugLog', 'org.mvplugins.multiverse.inventories.utils.DebugFileLogger'
    relocate 'com.dumptruckman.bukkit.configuration', 'org.mvplugins.multiverse.inventories.utils.configuration'
    relocate 'net.minidev', 'org.mvplugins.multiverse.inventories.utils.minidev'

    dependencies {
        exclude(dependency {
            it.moduleGroup == 'org.ow2.asm'
        })
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Multiverse/Multiverse-Inventories"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }

        maven {
            // todo: remove before mv5 release
            name = "multiverseBeta"
            url = "https://repo.c0ding.party/multiverse-beta"
            credentials(PasswordCredentials)
        }
    }
}
